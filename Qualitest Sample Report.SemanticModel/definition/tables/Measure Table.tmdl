table 'Measure Table'
	lineageTag: 22122c0d-1160-4b1a-bb41-0cf0c0806790

	/// Percentage of tests passed out of total tests run, calculated as Total Passed Tests divided by Total Tests.
	measure 'Pass Rate %' = DIVIDE ( [Total Passed Tests], [Total Tests], 0 )
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: e3ef0775-e7e0-46a0-b83a-fbf50379eb4c

	/// Total number of tests that did not pass, calculated as the difference between Total Tests and Passed Tests.
	measure 'Failed Tests' = SUM(QualitestData[TotalTests]) - SUM(QualitestData[PassedTests])
		formatString: 0
		lineageTag: e3bd176a-4293-477b-9e6d-b0b05ce2e442

	/// Average number of calendar days between a project's start and end date across all projects in context.
	measure 'Avg Project Duration Days' =
			
			AVERAGEX(
			    QualitestData,
			    DATEDIFF(QualitestData[StartDate], QualitestData[EndDate], DAY)
			)
		lineageTag: 6b5b7183-82c9-41f1-9821-752664cf0b6f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Count of projects with zero critical defects and a pass rate of 90% or higher, representing fully successful deliveries.
	measure 'Project Success Count' = ```
			
			SUMX(
			    QualitestData,
			    IF(
			        QualitestData[CriticalDefects] = 0 && 
			        DIVIDE(QualitestData[PassedTests], QualitestData[TotalTests], 0) >= 0.9,
			        1,
			        0
			    )
			)
			```
		formatString: 0
		lineageTag: 69c40bb3-a8e8-4ebe-9abf-e951e6b72ee2

	/// Distinct count of projects started within the current quarter-to-date period.
	measure 'Projects This Quarter' =
			
			CALCULATE(
			    DISTINCTCOUNT(QualitestData[ProjectID]),
			    DATESQTD(QualitestData[StartDate])
			)
		formatString: 0
		lineageTag: fae3058d-8921-4d8d-8fd0-ad640fd3a2a1

	/// Distinct count of projects started within the current month-to-date period based on the Calendar table.
	measure 'Projects This Month' = ```
			
			CALCULATE(
			    DISTINCTCOUNT(QualitestData[ProjectID]),
			    DATESMTD('Calendar'[Date])
			)
			
			```
		formatString: 0
		lineageTag: 23209c37-2a96-463a-84fc-6b766d025556

	/// Distinct count of all unique project IDs within the current filter context.
	measure 'Total Projects' = ```
			
			DISTINCTCOUNT(QualitestData[ProjectID])
			
			
			```
		formatString: #,0
		lineageTag: db68c464-672c-49fc-8d19-dbf73e16c031

	/// Month-to-date pass rate calculated as the ratio of passed tests to total tests using Calendar date context.
	measure 'Pass Rate' =
			
			VAR TotalTests =
			    CALCULATE( SUM(QualitestData[TotalTests]), DATESMTD('Calendar'[Date]) )
			VAR TotalPassed =
			    CALCULATE( SUM(QualitestData[PassedTests]), DATESMTD('Calendar'[Date]) )
			RETURN
			DIVIDE(TotalPassed, TotalTests, 0)
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 6e12f227-1e5a-4d72-8909-13c9da68353e

	/// Alias of Pass Rate displayed with one decimal percentage format; used in visuals that require a distinct measure reference.
	measure 'Avg Pass Rate %' = [Pass Rate]
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: ac73f0bd-2606-4410-998a-750064b7b458

	/// Month-to-date average customer satisfaction score on a 1–5 scale, scoped to the Calendar month-to-date context.
	measure 'Avg CSAT' =
			
			CALCULATE(
			    AVERAGE( QualitestData[CustomerSatisfaction (1–5)] ),
			    DATESMTD( 'Calendar'[Date] )
			)
		lineageTag: f2e9eba8-3f7a-4269-af01-df73b446911b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average automation rate expressed as a decimal percentage, derived by dividing the raw automation rate field by 100.
	measure 'Avg Automation Rate %' = ```
			DIVIDE( AVERAGE(QualitestData[AutomationRate (%)]), 100 )
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: ed4f86ef-37cc-4fa8-a1cd-cc9bad6f7e08

	/// Test-volume-weighted average defect density, expressing the number of defects per 1,000 tests run across all projects.
	measure 'Defects per 1000' =
			
			VAR TotalTests = SUM(QualitestData[TotalTests])
			VAR WeightedDefects = SUMX(QualitestData, QualitestData[DefectsPer1000] * QualitestData[TotalTests])
			RETURN
			DIVIDE(WeightedDefects, TotalTests, 0)
		lineageTag: 8e7863b0-cfdd-4005-976b-20a4a0b62772

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average on-time delivery rate across projects, expressed as a decimal percentage by dividing the raw field by 100.
	measure 'Ontime Delivery %' = ```
			
			DIVIDE( AVERAGE( QualitestData[OnTimeDelivery (%)] ), 100 )
			
			
			```
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: e0c92a51-d251-4a50-8a19-186f3cfce908

	/// Sum of all tests executed across projects in the current filter context.
	measure 'Total Tests' = SUM(QualitestData[TotalTests])
		formatString: #,0
		lineageTag: 7dd6d94f-78a6-4feb-af53-8fe6c80d3144

	/// Sum of all tests that passed across projects in the current filter context.
	measure 'Total Passed Tests' = SUM(QualitestData[PassedTests])
		formatString: #,0
		lineageTag: 618633e9-3057-45a0-8d1c-923dd6b99342

	/// Sum of critical defects logged across all projects in the current filter context.
	measure 'Total Critical Defects' = SUM(QualitestData[CriticalDefects])
		formatString: #,0
		lineageTag: abab3264-76d5-41bd-886d-e4c95a464077

	/// Composite quality index combining pass rate (up to 4 pts), CSAT (up to 6 pts), and a defect density penalty (up to -2 pts).
	measure 'Quality Score' =
			
			VAR PassRateScore = [Pass Rate] * 4  // Max 4 points
			VAR SatisfactionScore = [Avg CSAT] * 1.5  // Max 6 points (4.0 * 1.5 = 6)
			VAR DefectPenalty = MIN(1, DIVIDE([Defects per 1000], 50, 0)) * 2  // Max 2 point penalty
			RETURN
			ROUND(PassRateScore + SatisfactionScore - DefectPenalty, 2)
		lineageTag: 28178ca1-7878-4e62-8fb1-278130b8359d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Ratio of projects with zero critical defects and at least 85% pass rate to total distinct projects.
	measure 'Project Success Rate' = ```
			VAR SuccessfulProjects = SUMX( QualitestData, IF( QualitestData[CriticalDefects] = 0 && DIVIDE(QualitestData[PassedTests], QualitestData[TotalTests], 0) >= 0.85, 1, 0 ) ) VAR TotalProjects = DISTINCTCOUNT(QualitestData[ProjectID]) RETURN DIVIDE(SuccessfulProjects, TotalProjects, 0)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 4856a832-4a34-49d1-b277-23cad41f9166

	/// Stricter project success rate requiring ≤7 critical defects and ≥95% pass rate per project, aggregated to a percentage.
	measure 'Project Success Rate %' = ```
			
			VAR Proj =
			    SUMMARIZE(
			        QualitestData, QualitestData[ProjectID],
			        "Crit", SUM ( QualitestData[CriticalDefects] ),
			        "Pass", DIVIDE( SUM(QualitestData[PassedTests]), SUM(QualitestData[TotalTests]), 0 )
			    )
			VAR Success =
			    COUNTROWS ( FILTER ( Proj, [Crit] <= 7 && [Pass] >= 0.95 ) )
			RETURN
			DIVIDE ( Success, COUNTROWS(Proj), 0 )
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: b26df309-db06-488a-bc2c-db6a576db105

	/// Difference between the current pass rate and the industry-wide average pass rate, ignoring all filters except Industry.
	measure 'Pass Rate vs Industry Avg' = ```
			
			VAR CurrentPassRate = [Pass Rate]
			VAR IndustryAverage = 
			    CALCULATE(
			        [Pass Rate],
			        ALLEXCEPT(QualitestData, QualitestData[Industry])
			    )
			RETURN
			CurrentPassRate - IndustryAverage
			```
		lineageTag: d81d8f30-2040-400d-afa9-6e56988a5d71

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Difference between the current CSAT score and the regional average CSAT, removing all filters except Region.
	measure 'Satisfaction vs Region Avg' = ```
			
			VAR CurrentSatisfaction = [Avg CSAT]
			VAR RegionAverage = 
			    CALCULATE(
			        [Avg CSAT],
			        ALLEXCEPT(QualitestData, QualitestData[Region])
			    )
			RETURN
			CurrentSatisfaction - RegionAverage
			```
		lineageTag: 831de9e9-0246-4c80-94fa-516120397cc9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Rank of each industry by average automation rate, with rank 1 assigned to the highest automation rate.
	measure 'Automation Rate Rank' =
			
			RANKX(
			    ALL(QualitestData[Industry]),
			    CALCULATE([Avg Automation Rate]),
			    ,
			    DESC
			)
		formatString: 0
		lineageTag: 27e38bbe-8963-4385-b887-8d437e750c40

	/// Difference between the selected industry's Quality Score and the overall model-wide Quality Score.
	measure 'Industry vs Overall Performance' =
			
			VAR IndustryScore = [Quality Score]
			VAR OverallScore = CALCULATE([Quality Score], ALL(QualitestData))
			RETURN
			IndustryScore - OverallScore
		lineageTag: ba28cae4-90b0-4e45-afb7-19b795265aa3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Pass rate for the same period one month prior, used as the baseline for month-over-month pass rate comparisons.
	measure 'Pass Rate PM' =
			
			CALCULATE(
			    [Pass Rate],
			    DATEADD(QualitestData[StartDate], -1, MONTH)
			)
		lineageTag: d02260c6-87ed-4d2d-a78e-271670c88067

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Absolute change in pass rate between the current period and the prior month (Pass Rate minus Pass Rate PM).
	measure 'Pass Rate MoM Change' = [Pass Rate] - [Pass Rate PM]
		lineageTag: ebad9949-d955-46e8-8743-83c2e73a608b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Percentage change in pass rate relative to the prior month, expressed as a number (e.g., 2.5 for 2.5%).
	measure 'Pass Rate MoM Change %' =
			
			DIVIDE([Pass Rate MoM Change], [Pass Rate PM], 0) * 100
		lineageTag: 95f9a298-7138-4852-9444-12a4fb3376ba

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Pass rate calculated over a rolling 3-month window ending on the latest project start date in context.
	measure 'Rolling 3M Avg Pass Rate' = ```
			
			VAR Last3Months = 
			    DATESINPERIOD(
			        QualitestData[StartDate],
			        MAX(QualitestData[StartDate]),
			        -3,
			        MONTH
			    )
			RETURN
			CALCULATE([Pass Rate], Last3Months)
			```
		lineageTag: 25d31ffc-bbf0-484f-8658-0bd114e42877

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Cumulative distinct count of projects from the start of the current year through the latest date in context.
	measure 'YTD Projects' =
			
			CALCULATE(
			    [Total Projects],
			    DATESYTD(QualitestData[StartDate])
			)
		formatString: 0
		lineageTag: 57197ca0-779e-4761-b99c-35bf65893e24

	/// Total projects for the same period in the prior year, used as the YoY comparison baseline.
	measure 'PYSP Projects' =
			
			CALCULATE(
			    [Total Projects],
			    SAMEPERIODLASTYEAR(QualitestData[StartDate])
			)
		formatString: 0
		lineageTag: 2eb5298c-e1b5-4a80-91b4-a186613f9a19

	/// Year-over-year project volume growth rate, comparing YTD projects to the same period last year.
	measure 'YoY Growth %' =
			
			VAR CurrentPeriod = [YTD Projects]
			VAR PreviousPeriod = [PYSP Projects]
			RETURN
			DIVIDE(CurrentPeriod - PreviousPeriod, PreviousPeriod, 0) * 100
		lineageTag: e32f1f74-7e08-4c7e-b49e-9672c177fb87

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Hex color code for conditional formatting of pass rate: green (≥90%), orange (≥80%), or red (below 80%).
	measure 'Pass Rate Color' =
			
			SWITCH(
			    TRUE(),
			    [Pass Rate] >= 0.90, "#27AE60",  // Green
			    [Pass Rate] >= 0.80, "#F39C12",  // Orange
			    "#E74C3C"  // Red
			)
		lineageTag: 363aab74-6c7e-40b8-be72-6a15dbc564a5

	/// Hex color code for conditional formatting of CSAT: green (≥4.0), orange (≥3.5), or red (below 3.5).
	measure 'Satisfaction Color' =
			
			SWITCH(
			    TRUE(),
			    [Avg CSAT] >= 4.0, "#27AE60",  // Green
			    [Avg CSAT] >= 3.5, "#F39C12",  // Orange
			    "#E74C3C"  // Red
			)
		lineageTag: 84f21946-067b-4081-8d34-12cb6a1e5e78

	/// Hex color code for conditional formatting of defect density: green (≤20/1000), orange (≤40/1000), or red (above 40/1000).
	measure 'Defect Color' =
			
			SWITCH(
			    TRUE(),
			    [Defects per 1000] <= 20, "#27AE60",  // Green
			    [Defects per 1000] <= 40, "#F39C12",  // Orange
			    "#E74C3C"  // Red
			)
		lineageTag: dff07434-d4d6-4ee0-a5d8-258b24d17323

	/// Hex color code for conditional formatting of automation rate: green (≥70%), orange (≥50%), or red (below 50%).
	measure 'Automation Color' =
			
			SWITCH(
			    TRUE(),
			    [Avg Automation Rate] >= 70, "#27AE60",  // Green
			    [Avg Automation Rate] >= 50, "#F39C12",  // Orange
			    "#E74C3C"  // Red
			)
		lineageTag: a2821ea7-d00c-4983-bd5a-f0fa3498bbd0

	/// Pass rate returned only for the top 5 clients by pass rate ranking; returns BLANK for all other clients.
	measure 'Top 5 Clients Pass Rate' = ```
			
			VAR CurrentClient = SELECTEDVALUE(QualitestData[ClientName])
			VAR ClientRank = 
			    RANKX(
			        ALL(QualitestData[ClientName]),
			        CALCULATE([Pass Rate]),
			        ,
			        DESC
			    )
			RETURN
			IF(ClientRank <= 5, [Pass Rate], BLANK())
			```
		lineageTag: 516c763e-b9bc-4be9-9f48-7103cf020ef8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average CSAT returned only for the bottom 5 clients by satisfaction ranking; returns BLANK for all others.
	measure 'Bottom 5 Clients Satisfaction' = ```
			
			VAR CurrentClient = SELECTEDVALUE(QualitestData[ClientName])
			VAR ClientRank = 
			    RANKX(
			        ALL(QualitestData[ClientName]),
			        CALCULATE([Avg CSAT]),
			        ,
			        ASC
			    )
			RETURN
			IF(ClientRank <= 5, [Avg CSAT], BLANK())
			```
		lineageTag: 1856effa-1192-402e-9282-afdbb2c5039d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Name of the region with the highest Quality Score, identified using TOPN and returned as a text string.
	measure 'Best Region' = ```
			
			VAR BestRegionName = 
			    TOPN(
			        1,
			        SUMMARIZE(QualitestData, QualitestData[Region]),
			        CALCULATE([Quality Score])
			    )
			RETURN
			CONCATENATEX(BestRegionName, QualitestData[Region])
			```
		lineageTag: ff3c7dcd-1d2f-46aa-ab6d-2cd992eb72b2

	/// Name of the service type with the lowest pass rate, identified using TOPN in ascending order and returned as text.
	measure 'Worst Service Type' = ```
			
			VAR WorstService = 
			    TOPN(
			        1,
			        SUMMARIZE(QualitestData, QualitestData[ServiceType]),
			        CALCULATE([Pass Rate]),
			        ASC
			    )
			RETURN
			CONCATENATEX(WorstService, QualitestData[ServiceType])
			```
		lineageTag: cbfcef8c-fa6b-4af7-bac1-5bb78d82148f

	/// Count of projects where the automation rate is 70% or higher, filtered directly on the source data.
	measure 'High Automation Projects' =
			
			CALCULATE(
			    [Total Projects],
			    QualitestData[AutomationRate (%)] >= 70
			)
		formatString: 0
		lineageTag: 02a92dc9-3220-43f2-a35d-e7c74f2c3559

	/// Percentage of total projects that have an automation rate of 70% or higher.
	measure 'High Automation Projects %' =
			
			DIVIDE([High Automation Projects], [Total Projects], 0) * 100
		lineageTag: 6b272a94-626e-4c61-999e-398a1ee76461

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Count of projects with more than 10 critical defects, flagging high-risk deliveries.
	measure 'Critical Projects' = ```
			
			CALCULATE(
			    [Total Projects],
			    QualitestData[CriticalDefects] > 10
			)
			
			```
		formatString: 0
		lineageTag: fa7165ce-a297-4c33-b38b-4f8aafe22875

	/// Percentage of total projects classified as critical due to more than 10 critical defects.
	measure 'Critical Projects %' =
			
			DIVIDE([Critical Projects], [Total Projects], 0) * 100
		lineageTag: 6a272c32-7476-4b32-8e84-be083c346e74

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Estimated financial exposure from critical defects, assuming a fixed cost of $1,000 per critical defect.
	measure 'Revenue Impact' =
			
			VAR CostPerDefect = 1000  // Assume $1000 per critical defect
			VAR TotalCriticalDefects = SUM(QualitestData[CriticalDefects])
			RETURN
			TotalCriticalDefects * CostPerDefect
		formatString: 0
		lineageTag: 68eb2dbd-51dd-4b6a-99f7-661957876820

	/// Weighted composite score combining automation rate (30%), pass rate (50%), and CSAT (20%) into a single efficiency index.
	measure 'Efficiency Score' =
			
			VAR AutomationBonus = [Avg Automation Rate] * 30
			VAR QualityScore = [Pass Rate] * 50
			VAR SatisfactionScore = DIVIDE([Avg CSAT], 5, 0) * 20
			RETURN
			ROUND(AutomationBonus + QualityScore + SatisfactionScore, 1)
		lineageTag: 6f9dfad6-7759-4434-9270-c766f47f9cfe

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average number of tests executed per project, calculated as Total Tests divided by Total Projects.
	measure 'Avg Tests per Project' =
			
			DIVIDE([Total Tests], [Total Projects], 0)
		lineageTag: 4c8e150d-3d54-4e71-be6b-882bd470aa4c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Proxy return-on-investment index combining the product of automation rate and pass rate with a CSAT-weighted quality benefit.
	measure 'Automation ROI' =
			
			VAR AutomationBenefit = [Avg Automation Rate] * [Pass Rate]
			VAR QualityBenefit = [Avg CSAT] * 0.2
			RETURN
			ROUND(AutomationBenefit + QualityBenefit, 2)
		lineageTag: 92bde5d7-d260-4cc6-a558-3f359ea6fa2c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label combining the average pass rate percentage and total project count, used in chart annotations.
	measure 'Pass Rate Label' =
			
			FORMAT([Avg Pass Rate %], "0.0%") & " (" & [Total Projects] & ")"
		lineageTag: 80e6840b-fe8b-4d7b-9d13-6388f16e5d48

	/// Multi-line tooltip string displaying service type, pass rate, total projects, and average tests per project for visual tooltips.
	measure 'Service Type Tooltip' =
			
			"Service Type: " & SELECTEDVALUE(QualitestData[ServiceType]) &
			UNICHAR(10) & "Pass Rate: " & FORMAT([Avg Pass Rate %], "0.0%") &
			UNICHAR(10) & "Total Projects: " & [Total Projects] &
			UNICHAR(10) & "Avg Tests per Project: " & FORMAT([Avg Tests per Project], "#,##0")
		lineageTag: 48561661-fd4f-40b0-9fb7-6b8de5a16f35

	/// Customer satisfaction score expressed as a percentage of the maximum 5-point scale (score divided by 5).
	measure 'Avg Customer Satisfaction %' =
			
			DIVIDE( AVERAGE(QualitestData[CustomerSatisfaction (1–5)]), 5 )
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: c0edd5ee-bd24-4ad3-bd61-851174ead191

	/// Count of distinct project IDs visible in the current row/filter context using VALUES, respecting all active filters.
	measure 'Projects in Context' = ```
			COUNTROWS ( VALUES ( QualitestData[ProjectID] ) )
			
			
			
			```
		lineageTag: 1912fde4-accb-4780-8fe1-b9c1e42eb37c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Count of projects in the current context meeting the strict success criteria of ≤5 critical defects and ≥95% pass rate.
	measure 'Successful Projects in Context' =
			
			SUMX (
			    VALUES ( QualitestData[ProjectID] ),
			    VAR crit = CALCULATE ( SUM ( QualitestData[CriticalDefects] ) )
			    VAR passRate = CALCULATE ( DIVIDE ( SUM ( QualitestData[PassedTests] ),
			                                        SUM ( QualitestData[TotalTests] ), 0 ) )
			    RETURN IF ( crit <= 5 && passRate >= 0.95, 1, 0 )
			)
		lineageTag: 46e827bd-b9bb-4c93-8867-6265637346a1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Alternative success rate using context-aware project counts; ratio of Successful Projects in Context to Projects in Context.
	measure 'Project Success Rate1 %' =
			
			VAR Proj = VALUES(QualitestData[ProjectID])
			VAR Succ = [Successful Projects in Context]
			VAR Tot  = [Projects in Context]
			RETURN COALESCE( DIVIDE( Succ, Tot ), 0 )
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 764122b1-5d58-420f-8787-763c515dd4d7

	/// Proportion of total defects found that are classified as critical, indicating the severity concentration of defects.
	measure 'Critical Defect Rate %' = ```
			
			DIVIDE( SUM(QualitestData[CriticalDefects]),
			        SUM(QualitestData[DefectsFound]), 0 )
			
			
			```
		lineageTag: ef3bb533-2caa-47b9-95ab-d315fa8ec1f9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Static benchmark value of 98% representing the target pass rate threshold for performance gap analysis.
	measure 'Pass Target' = 0.98
		lineageTag: 24e6178d-8afa-47c6-aad5-e953f144ff4b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Static benchmark value of 95% representing the target on-time delivery rate for performance gap analysis.
	measure 'OnTime Target' = 0.95
		lineageTag: 04b65119-6a03-4522-af06-cbbe4af67b59

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Static benchmark value of 65% representing the target automation rate for performance gap analysis.
	measure 'Automation Target' = 0.65
		lineageTag: fb08dbe1-5f91-4f00-ac44-78adc9f2d425

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Static benchmark value of 85% representing the target customer satisfaction rate for performance gap analysis.
	measure 'CSAT Target' = 0.85
		lineageTag: f99ece59-53d9-4cff-9f9e-e0249cdde6b1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Difference in percentage points between the actual pass rate and the 98% pass rate target; negative means below target.
	measure 'Pass Gap (pp)' = [Pass Rate %] - [Pass Target]
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: 49f5adc8-bdb1-47d8-b594-0ffc5ac8055e

	/// Difference in percentage points between the actual on-time delivery rate and the 95% on-time target.
	measure 'On-Time Gap (pp)' = [Ontime Delivery %] - [OnTime Target]
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: 2cd9867d-aa97-47f9-af65-56b0fb9b502a

	/// Difference in percentage points between the actual automation rate and the 65% automation target.
	measure 'Automation Gap (pp)' = [Avg Automation Rate %] - [Automation Target]
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: d06eb75a-fca7-4909-b3eb-af92f8365362

	/// Difference in percentage points between the actual customer satisfaction percentage and the 85% CSAT target.
	measure 'CSAT Gap (pp)' = [Avg Customer Satisfaction %] - [CSAT Target]
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: 179333e3-5fbd-44a5-a9e2-a5d278c55568

	/// Constant value of zero used as a reference baseline in variance and gap charts.
	measure 'Zero Line' = ```
			0
			
			```
		formatString: 0
		lineageTag: b75bd56a-ea1e-42e2-9cf8-9e9026051262

	/// Distinct count of projects started in the prior calendar month, scoped to the prior month-to-date using Calendar context.
	measure 'Projects Last Month' = ```
			
			CALCULATE(
			    DISTINCTCOUNT(QualitestData[ProjectID]),
			    DATESMTD(
			        DATEADD('Calendar'[Date], -1, MONTH)
			    )
			)
			
			```
		formatString: 0
		lineageTag: 39be12ea-26f5-4c53-b92f-38eb2a13069d

	/// Absolute month-over-month change in project count between the current month and the prior month.
	measure 'Projects MoM Δ' =
			
			[Projects This Month] - [Projects Last Month]
		formatString: 0
		lineageTag: 59ee95e9-0f72-4f93-89cf-c0576dae8c7e

	/// Formatted text label showing the MoM project count change with directional arrow (▲/▼/▬) for card visuals.
	measure 'Projects MoM Label' =
			
			VAR d = [Projects MoM Δ]
			VAR arrow = IF(d > 0, UNICHAR(9650), IF(d < 0, UNICHAR(9660), UNICHAR(9644)))
			RETURN FORMAT(d, "+#,0;-#,0;0") & " vs last month " & arrow
		lineageTag: d0a174e6-07ed-4229-8016-cafceb9683c0

	/// Hex color code reflecting the direction of MoM project count change: green for growth, red for decline, grey for flat.
	measure 'Projects MoM Color' = ```
			
			VAR d = [Projects MoM Δ]
			RETURN
			    IF ( d > 0, "#059669", IF ( d < 0, "#DC2626", "#6B7280" ) )
			
			```
		lineageTag: a16b55e5-e84e-41ca-905e-d0f924ba5934

	/// Distinct count of all projects across all calendar periods, ignoring any date filter context from the Calendar table.
	measure 'Total Projects (All Time)' = ```
			
			CALCULATE(
			    DISTINCTCOUNT(QualitestData[ProjectID]),
			    ALL('Calendar')
			)
			
			```
		formatString: 0
		lineageTag: fc51fa8e-bde0-4dae-84a4-e5023b512537

	/// Month-to-date pass rate for the prior calendar month, used as the baseline in MoM pass rate comparisons.
	measure 'Pass Rate (Last Month)' =
			
			VAR TotalTests =
			    CALCULATE( SUM(QualitestData[TotalTests]),
			               DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) ) )
			VAR TotalPassed =
			    CALCULATE( SUM(QualitestData[PassedTests]),
			               DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) ) )
			RETURN
			DIVIDE(TotalPassed, TotalTests, 0)
		lineageTag: 9dca2e36-502a-4c19-93ef-8196f2cbdde8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Absolute change in pass rate between the current month and the prior month (current minus last month).
	measure 'Pass Rate Δ' =
			
			[Pass Rate] - [Pass Rate (Last Month)]
		lineageTag: 4cefa81f-94c8-45da-96ed-da9ded366ea5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label showing the pass rate MoM change as a percentage with a directional arrow for KPI cards.
	measure 'Pass Rate Δ Label' =
			
			VAR d = [Pass Rate Δ]
			VAR arrow =
			    IF(d > 0, UNICHAR(9650), IF(d < 0, UNICHAR(9660), UNICHAR(9644)))
			RETURN
			FORMAT(d, "+0.0%;-0.0%;0.0%") & " vs last month " & arrow
		lineageTag: ea2e7664-a74a-4ec8-ae96-39556df8a0a9

	/// Hex color code for pass rate MoM change: green for improvement, red for decline, grey for no change.
	measure 'Pass Rate Δ Color' =
			
			VAR d = [Pass Rate Δ]
			RETURN
			    IF(d > 0, "#059669", IF(d < 0, "#DC2626", "#6B7280"))
		lineageTag: 77601a25-3f00-41f9-ba3e-d361e1f1bd88

	/// Average customer satisfaction score for the prior calendar month, used as the CSAT MoM comparison baseline.
	measure 'Avg CSAT (Last Month)' =
			
			CALCULATE(
			    AVERAGE( QualitestData[CustomerSatisfaction (1–5)] ),
			    DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) )
			)
		lineageTag: 9669df10-4297-49dd-968f-1590247d0bf7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Absolute change in average CSAT score between the current month and the prior month.
	measure 'CSAT Δ' =
			
			[Avg CSAT] - [Avg CSAT (Last Month)]
		lineageTag: 1c0b9215-35dc-4e8f-89ee-797f4d860b18

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Relative percentage change in CSAT between the current and prior month, expressed as a decimal ratio.
	measure 'CSAT % Δ' =
			
			DIVIDE( [Avg CSAT] - [Avg CSAT (Last Month)], [Avg CSAT (Last Month)] )
		lineageTag: ce130f6e-e926-406c-ae34-be27dbd5ba43

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label showing the absolute CSAT change with directional arrow, suitable for KPI card displays.
	measure 'CSAT Δ Label' =
			
			VAR d = [CSAT Δ]
			VAR arrow = IF( d > 0, UNICHAR(9650), IF( d < 0, UNICHAR(9660), UNICHAR(9644) ) )
			RETURN FORMAT( d, "+0.00;-0.00;0.00" ) & " vs last month " & arrow
		lineageTag: 40640e4b-a879-4fd8-9a00-fc200708d07e

	/// Combined label showing both the absolute and percentage CSAT change with a directional arrow vs. the prior month.
	measure 'CSAT Δ Label (Abs + %)' =
			
			VAR dAbs = [CSAT Δ]
			VAR dPct = [CSAT % Δ]
			VAR arrow = IF( dAbs > 0, UNICHAR(9650), IF( dAbs < 0, UNICHAR(9660), UNICHAR(9644) ) )
			RETURN FORMAT(dAbs, "+0.00;-0.00;0.00") & " (" &
			       FORMAT(dPct, "+0.0%;-0.0%;0.0%") & ") vs last month " & arrow
		lineageTag: 16facd3b-fc27-431f-91d3-645105af028c

	/// Hex color code for CSAT MoM change: green for improvement, red for decline, grey for no change.
	measure 'CSAT Δ Color' =
			
			VAR d = [CSAT Δ]
			RETURN IF( d > 0, "#059669", IF( d < 0, "#DC2626", "#6B7280" ) )
		lineageTag: 610c50b4-823e-4299-9278-02780b0675f7

	/// Simple average of the raw automation rate field (in percent units, e.g. 75 for 75%) across all projects in context.
	measure 'Avg Automation Rate' = AVERAGE(QualitestData[AutomationRate (%)])
		lineageTag: a5727b24-8c91-4764-a5e0-78fe6de35afc

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average automation rate for the current month-to-date period, expressed as a decimal percentage by dividing by 100.
	measure 'Automation (This Month)' =
			
			VAR ThisMonthVal =
			    CALCULATE(
			        AVERAGE(QualitestData[AutomationRate (%)]),
			        DATESMTD('Calendar'[Date])
			    )
			RETURN DIVIDE(ThisMonthVal, 100, 0)
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 7c5d7438-e790-44b3-862f-b7fcbc7acf02

	/// Average automation rate for the prior calendar month-to-date period, expressed as a decimal percentage.
	measure 'Automation (Last Month)' =
			
			VAR LastMonthVal =
			    CALCULATE(
			        AVERAGE(QualitestData[AutomationRate (%)]),
			        DATESMTD(DATEADD('Calendar'[Date], -1, MONTH))
			    )
			RETURN DIVIDE(LastMonthVal, 100, 0)
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: da084d33-d2af-4a75-bd2b-9d78c5eb8826

	/// Absolute change in automation rate between the current month and the prior month.
	measure 'Automation Δ' =
			
			[Automation (This Month)] - [Automation (Last Month)]
		lineageTag: c51ae324-a437-4a50-997c-900fac0a2508

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label showing the automation rate MoM change as a percentage with a directional arrow.
	measure 'Automation Δ Label' =
			
			VAR d = [Automation Δ]
			VAR arrow = IF(d > 0, UNICHAR(9650), IF(d < 0, UNICHAR(9660), UNICHAR(9644)))
			RETURN FORMAT(d, "+0.0%;-0.0%;0.0%") & " vs last month " & arrow
		lineageTag: cc5d23f0-0494-4821-87b5-4c2dc7cf17ea

	/// Hex color code for automation rate MoM change: green for improvement, red for decline, grey for no change.
	measure 'Automation Δ Color' =
			
			VAR d = [Automation Δ]
			RETURN
			    IF(d > 0, "#059669", IF(d < 0, "#DC2626", "#6B7280"))
		lineageTag: bbd8a509-241d-477b-b494-473d90fba185

	/// Test-volume-weighted defect density for the current month-to-date, expressed as defects per 1,000 tests.
	measure 'Defects/1000 (This Month)' =
			
			VAR TotalTests_MTD =
			    CALCULATE( SUM(QualitestData[TotalTests]), DATESMTD('Calendar'[Date]) )
			VAR WeightedDefects_MTD =
			    CALCULATE(
			        SUMX(QualitestData, QualitestData[DefectsPer1000] * QualitestData[TotalTests]),
			        DATESMTD('Calendar'[Date])
			    )
			RETURN DIVIDE(WeightedDefects_MTD, TotalTests_MTD, 0)
		lineageTag: 0c5f1edb-357a-4ec7-b0ba-f3905623f815

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Test-volume-weighted defect density for the prior calendar month-to-date, used as the MoM comparison baseline.
	measure 'Defects/1000 (Last Month)' =
			
			VAR TotalTests_LM =
			    CALCULATE( SUM(QualitestData[TotalTests]),
			               DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) ) )
			VAR WeightedDefects_LM =
			    CALCULATE(
			        SUMX(QualitestData, QualitestData[DefectsPer1000] * QualitestData[TotalTests]),
			        DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) )
			    )
			RETURN DIVIDE(WeightedDefects_LM, TotalTests_LM, 0)
		lineageTag: 3595682c-5fc9-4588-bc69-228a4c74823e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Absolute change in defect density between the current and prior month (positive means more defects).
	measure 'Defects/1000 Δ' = ```
			
			[Defects/1000 (This Month)] - [Defects/1000 (Last Month)]
			
			```
		lineageTag: 5f3bb6bc-5c18-4ac2-8b45-31d7a37732a3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Relative percentage change in defect density between the current and prior month.
	measure 'Defects/1000 % Δ' =
			
			DIVIDE( [Defects/1000 Δ], [Defects/1000 (Last Month)] )
		lineageTag: 18aac5ca-6891-4f15-adcc-5a842ed33dd0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label showing the defect density MoM change with a directional arrow (inverted: down is good).
	measure 'Defects/1000 Δ Label' =
			
			VAR d = [Defects/1000 Δ]
			VAR arrow = IF(d < 0, UNICHAR(9660), IF(d > 0, UNICHAR(9650), UNICHAR(9644)))
			RETURN FORMAT(d, "+0.00;-0.00;0.00") & " vs last month " & arrow
		lineageTag: 94fb91e6-e30c-4a32-924d-68dc8638d2ca

	/// Combined label showing both the absolute and percentage change in defect density with directional arrow vs. prior month.
	measure 'Defects/1000 Δ Label (Abs + %)' =
			
			VAR dAbs = [Defects/1000 Δ]
			VAR dPct = [Defects/1000 % Δ]
			VAR arrow = IF(dAbs < 0, UNICHAR(9660), IF(dAbs > 0, UNICHAR(9650), UNICHAR(9644)))
			RETURN FORMAT(dAbs, "+0.00;-0.00;0.00") & " (" &
			       FORMAT(dPct, "+0.0%;-0.0%;0.0%") & ") vs last month " & arrow
		lineageTag: 2bb02a0f-1d1d-4178-a1a9-a587f612560d

	/// Hex color code for defect density MoM change: green for decrease, red for increase, grey for no change.
	measure 'Defects/1000 Δ Color' =
			
			VAR d = [Defects/1000 Δ]
			RETURN IF(d < 0, "#059669", IF(d > 0, "#DC2626", "#6B7280"))
		lineageTag: 37555c00-dd38-43c4-8d49-a7d2c37aeffe

	/// Average on-time delivery rate for the current month-to-date, expressed as a decimal percentage.
	measure 'Ontime (This Month)' =
			
			VAR ThisMonthVal =
			    CALCULATE(
			        AVERAGE(QualitestData[OnTimeDelivery (%)]),
			        DATESMTD('Calendar'[Date])
			    )
			RETURN DIVIDE(ThisMonthVal, 100, 0)
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 63377494-1409-415b-ab8c-40fa3ca2c248

	/// Average on-time delivery rate for the prior calendar month-to-date, used as the MoM comparison baseline.
	measure 'Ontime (Last Month)' =
			
			VAR LastMonthVal =
			    CALCULATE(
			        AVERAGE(QualitestData[OnTimeDelivery (%)]),
			        DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) )
			    )
			RETURN DIVIDE(LastMonthVal, 100, 0)
		lineageTag: 3ca6a375-40d6-4b33-adc9-baf49feb0aa0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Absolute change in on-time delivery rate between the current month and the prior month.
	measure 'Ontime Δ' =
			
			[Ontime (This Month)] - [Ontime (Last Month)]
		lineageTag: 058b5464-8731-4591-8b64-7adcb697f7d4

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Relative percentage change in on-time delivery rate between the current and prior month.
	measure 'Ontime % Δ' =
			
			DIVIDE([Ontime Δ], [Ontime (Last Month)])
		lineageTag: e131f883-5054-4949-b3e5-f425fdfe881a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label showing the on-time delivery rate MoM change as a percentage with a directional arrow.
	measure 'Ontime Δ Label' =
			
			VAR d = [Ontime Δ]
			VAR arrow =
			    IF(d > 0, UNICHAR(9650), IF(d < 0, UNICHAR(9660), UNICHAR(9644)))
			RETURN FORMAT(d, "+0.0%;-0.0%;0.0%") & " vs last month " & arrow
		lineageTag: 6674b6fb-af5a-4b53-9666-e7d7a6e8e1ee

	/// Combined label showing both absolute and percentage on-time delivery rate change with directional arrow vs. prior month.
	measure 'Ontime Δ Label (Abs + %)' =
			
			VAR dAbs = [Ontime Δ]
			VAR dPct = [Ontime % Δ]
			VAR arrow =
			    IF(dAbs > 0, UNICHAR(9650), IF(dAbs < 0, UNICHAR(9660), UNICHAR(9644)))
			RETURN FORMAT(dAbs, "+0.0%;-0.0%;0.0%") & " (" &
			       FORMAT(dPct, "+0.0%;-0.0%;0.0%") & ") vs last month " & arrow
		lineageTag: 569904c8-67ae-427d-a34c-53e623277759

	/// Hex color code for on-time delivery MoM change: green for improvement, red for decline, grey for no change.
	measure 'Ontime Δ Color' =
			
			VAR d = [Ontime Δ]
			RETURN IF(d > 0, "#059669", IF(d < 0, "#DC2626", "#6B7280"))
		lineageTag: ba5f1c72-4097-4f9e-949b-feae58397e0c

	/// Total critical defects logged within the current month-to-date period based on the Calendar table.
	measure 'Critical Defects (This Month)' =
			
			CALCULATE(
			    SUM(QualitestData[CriticalDefects]),
			    DATESMTD('Calendar'[Date])
			)
		formatString: 0
		lineageTag: 4cecb1d6-cf12-42de-ad75-aa5d32785c89

	/// Total critical defects for the prior calendar month-to-date, used as the MoM comparison baseline.
	measure 'Critical Defects (Last Month)' =
			
			CALCULATE(
			    SUM(QualitestData[CriticalDefects]),
			    DATESMTD( DATEADD('Calendar'[Date], -1, MONTH) )
			)
		formatString: 0
		lineageTag: f21c2189-560b-4449-a76e-d1266f83da60

	/// Absolute change in critical defect count between the current and prior month (positive means more defects).
	measure 'Critical Defects Δ' =
			
			[Critical Defects (This Month)] - [Critical Defects (Last Month)]
		formatString: 0
		lineageTag: ecae8522-85ac-4820-a5dc-fa95b9df6d4f

	/// Relative percentage change in critical defect count between the current and prior month.
	measure 'Critical Defects % Δ' =
			
			DIVIDE( [Critical Defects Δ], [Critical Defects (Last Month)] )
		lineageTag: 21a09ed8-5c8f-4f3e-8303-1cdecc75bd55

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Formatted text label showing the critical defect count MoM change with directional arrow (inverted: down is good).
	measure 'Critical Defects Δ Label' =
			
			VAR d = [Critical Defects Δ]
			VAR arrow = IF( d < 0, UNICHAR(9660), IF( d > 0, UNICHAR(9650), UNICHAR(9644) ) )
			RETURN FORMAT( d, "+#,0;-#,0;0" ) & " vs last month " & arrow
		lineageTag: 03f0f0ec-01f5-4653-be01-f82d606f796b

	/// Combined label showing both absolute and percentage critical defect change with directional arrow vs. prior month.
	measure 'Critical Defects Δ Label (Abs + %)' =
			
			VAR dAbs = [Critical Defects Δ]
			VAR dPct = [Critical Defects % Δ]
			VAR arrow = IF( dAbs < 0, UNICHAR(9660), IF( dAbs > 0, UNICHAR(9650), UNICHAR(9644) ) )
			RETURN FORMAT(dAbs, "+#,0;-#,0;0") & " (" &
			       FORMAT(dPct, "+0.0%;-0.0%;0.0%") & ") vs last month " & arrow
		lineageTag: ed210c94-18c8-42c2-8acc-b58db972060e

	/// Hex color code for critical defect MoM change: green for decrease, red for increase, grey for no change.
	measure 'Critical Defects Δ Color' =
			
			VAR d = [Critical Defects Δ]
			RETURN IF( d < 0, "#059669", IF( d > 0, "#DC2626", "#6B7280" ) )
		lineageTag: ce982dd2-8f5a-4ebf-9cff-6ae23cade88e

	/// Relative percentage change in project count between the current and prior month, expressed as a decimal ratio.
	measure 'Projects % Δ' =
			
			DIVIDE([Projects MoM Δ], [Projects Last Month])
		lineageTag: fa87753e-4225-4747-a1b0-66f0021e3565

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Combined label showing both absolute and percentage project count change with directional arrow vs. the prior month.
	measure 'Projects Δ Label (Abs + %)' =
			
			VAR dAbs = [Projects MoM Δ]
			VAR dPct = [Projects % Δ]
			VAR arrow = IF(dAbs > 0, UNICHAR(9650), IF(dAbs < 0, UNICHAR(9660), UNICHAR(9644)))
			RETURN
			    FORMAT(dAbs, "+#,0;-#,0;0") & " (" &
			    FORMAT(dPct, "+0.0%;-0.0%;0.0%") & ") vs last month " & arrow
		lineageTag: 6d22740b-87c0-4e23-8fc5-2471d2c8de07

	/// Simple unfiltered average of the customer satisfaction score (1–5 scale) across all projects in context.
	measure 'Avg Customer Satisfaction' = AVERAGE(QualitestData[CustomerSatisfaction (1–5)])
		lineageTag: 2bed5c80-77e2-4e10-9b4c-579b61a2f316

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Returns current month project count only when the calendar month matches the selected MarkerMonth, used to place a chart marker point.
	measure 'Projects Marker (Disconnected)' = ```
			
			VAR SelMonth =
			    COALESCE( SELECTEDVALUE(MarkerMonth[MonthName]),
			              MAX('Calendar'[MonthName]) )  -- default to latest visible month
			RETURN
			IF(
			    ISINSCOPE('Calendar'[MonthName]) &&
			    MAX('Calendar'[MonthName]) = SelMonth,
			    [Projects This Month]
			)
			
			
			```
		formatString: 0
		lineageTag: bf9b4827-c9c6-4fd9-bdf4-2d91e15bab5c

	/// Total projects filtered to the MonthYear selected in the disconnected MarkerMonth table, enabling line chart context bridging.
	measure 'Total Projects for Line Chart' =
			
			CALCULATE(
			    [Total Projects],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		formatString: 0
		lineageTag: b2d744dd-06ff-45cb-9210-d138dd834d85

	/// Returns the project count only for the selected MarkerMonth on the line chart axis; used to highlight the current month data point.
	measure 'Current Month Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [Total Projects for Line Chart]
			)
		formatString: 0
		lineageTag: 85998694-6542-4062-b1d9-409631be9e4d

	/// Pass rate filtered to the MonthYear from the disconnected MarkerMonth table, enabling synchronized line chart navigation.
	measure 'Pass Rate % for Line Chart' =
			
			CALCULATE(
			    [Pass Rate],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 7bb16ab0-a299-4ba3-8be5-c6dbcc14049d

	/// Returns the pass rate only for the selected MarkerMonth on the axis; used to highlight the current month point on the pass rate trend line.
	measure 'CM Pass Rate Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [Pass Rate % for Line Chart]
			)
		lineageTag: 84a63d3c-5585-4aca-b249-17f66d1e79bc

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average CSAT filtered to the MonthYear from the disconnected MarkerMonth table for synchronized line chart display.
	measure 'CSAT for Line Chart' =
			
			CALCULATE(
			    [Avg CSAT],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		lineageTag: f89c5a8c-8521-4e54-a3b2-ebe71a816d1d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Returns the CSAT value only for the selected MarkerMonth; used to highlight the current month point on the CSAT trend line.
	measure 'CM CSAT Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [CSAT for Line Chart]
			)
		lineageTag: a9c86d3b-14e8-4428-b868-c293f9bb1899

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Automation rate for the current month filtered to the MarkerMonth selection, bridging the disconnected slicer to the line chart.
	measure 'Automation for Line Chart' =
			
			CALCULATE(
			     [Automation (This Month)],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		lineageTag: a83da968-6359-48fe-8f6d-b7daa1adba52

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Returns the automation rate only for the selected MarkerMonth; highlights the current month on the automation trend line.
	measure 'CM Automation Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [Automation for Line Chart]
			)
		lineageTag: e7136187-fca1-4300-a7f8-140ebe652fd3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Defect density for the current month filtered to the MarkerMonth selection, enabling line chart display via disconnected table.
	measure 'Defects/1000 for Line Chart' =
			
			CALCULATE(
			    [Defects/1000 (This Month)],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		lineageTag: 0260be33-f6ac-426e-9c09-fbe4a589cffb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Returns the defect density only for the selected MarkerMonth; highlights the current month point on the defects trend line.
	measure 'CM Defects/1000 Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [Defects/1000 for Line Chart]
			)
		lineageTag: 7e87e01d-5ef1-4bcd-8d61-f7d979ef59d1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// On-time delivery rate for the current month filtered to the MarkerMonth selection for synchronized line chart rendering.
	measure 'Ontime Delivery for Line Chart' =
			
			CALCULATE(
			    [Ontime (This Month)],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		lineageTag: 5668b8e8-6179-45e7-bb84-6ed8db08bae1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Returns the on-time delivery rate only for the selected MarkerMonth; highlights the current month on the delivery trend line.
	measure 'CM Ontime Delivery Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [Ontime Delivery for Line Chart]
			)
		lineageTag: 070a351c-08a2-4320-b0d4-89b95e332b93

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Critical defect count for the current month filtered to the MarkerMonth selection, bridging the disconnected table to the trend chart.
	measure 'Critical Defects for Line Chart' =
			
			CALCULATE(
			    [Critical Defects (This Month)],
			    TREATAS(VALUES(MarkerMonth[MonthYear]), 'Calendar'[MonthYear])
			    )
		formatString: 0
		lineageTag: 7e72883a-2ae7-418d-8fdd-bd54d2c48e21

	/// Returns critical defect count only for the selected MarkerMonth; highlights the current month point on the critical defects trend line.
	measure 'CM Critical Defencts Highlight' =
			
			IF(
			    SELECTEDVALUE('Calendar'[MonthYear]) = SELECTEDVALUE( MarkerMonth[MonthYear]),
			    [Critical Defects for Line Chart]
			)
		formatString: 0
		lineageTag: 690e2fd7-2531-4cc7-a07c-0f9bd25d0bf1

	column Column1
		dataType: string
		isHidden
		lineageTag: 869cdec0-586b-4598-80aa-c2ff1334fa00
		summarizeBy: none
		sourceColumn: Column1

		changedProperty = IsHidden

		annotation SummarizationSetBy = Automatic

	partition 'Measure Table' = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", type text}})
				in
				    #"Changed Type"

	annotation PBI_ResultType = Table

